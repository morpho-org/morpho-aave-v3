module MorphoInternal
use export int.MinMax
use int.ComputerDivision
use set.Fset
use set.FsetSum

use export pool.Pool

exception Unauthorized (* Thrown when trying to borrow/withdraw too much on Morpho *)

type morpho_storage = {
    mutable p2p_supply_amount: int; (* market.deltas.supply.scaledP2PTotal *)
    p2p_supply_balance: Map.t int; (* marketBalances.scaledP2PSupplyBalances() *)
    mutable p2p_supply_delta: int; (* market.deltas.supply.scaledDelta *)
    ghost mutable sum_pool_supply_balance: int;
    pool_supply_balance: Map.t int; (* marketBalances.scaledPoolSupplyBalances() *)

    mutable idle_supply: int; (* market.idleSupply *)
    ghost mutable sum_collateral_supply_balance: int;
    collateral_supply_balance: Map.t int; (* marketBalances.scaledCollateralBalances() *)

    mutable p2p_borrow_amount: int; (* market.deltas.borrow.scaledP2PTotal *)
    p2p_borrow_balance: Map.t int; (* marketBalances.scaledP2PBorrowBalances() *)
    mutable p2p_borrow_delta: int; (* market.deltas.borrow.scaledDelta *)
    ghost mutable sum_pool_borrow_balance: int;
    pool_borrow_balance: Map.t int; (* marketBalances.scaledPoolBorrowBalances() *)
} invariant {
    p2p_supply_amount = FsetSum.sum p2p_supply_balance.Map.domain (fun x -> get x p2p_supply_balance) &&
    forall a. get a p2p_supply_balance >= 0 &&
    0 <= p2p_supply_delta &&
    sum_pool_supply_balance = FsetSum.sum pool_supply_balance.Map.domain (fun x -> get x pool_supply_balance) &&
    forall a. get a pool_supply_balance >= 0 &&

    0 <= idle_supply &&
    sum_collateral_supply_balance = FsetSum.sum collateral_supply_balance.Map.domain (fun x -> get x collateral_supply_balance) &&
    forall a. get a collateral_supply_balance >= 0 &&

    p2p_borrow_amount = FsetSum.sum p2p_borrow_balance.Map.domain (fun x -> get x p2p_borrow_balance) &&
    forall a. get a p2p_borrow_balance >= 0 &&
    0 <= p2p_borrow_delta &&
    sum_pool_borrow_balance = FsetSum.sum pool_borrow_balance.Map.domain (fun x -> get x pool_borrow_balance) &&
    forall a. get a pool_borrow_balance >= 0
} by {
    p2p_supply_amount = 0;
    p2p_supply_balance = Map.create();
    p2p_supply_delta = 0;
    sum_pool_supply_balance = 0;
    pool_supply_balance = Map.create();

    idle_supply = 0;
    sum_collateral_supply_balance = 0;
    collateral_supply_balance = Map.create();

    p2p_borrow_amount = 0;
    p2p_borrow_balance = Map.create();
    p2p_borrow_delta = 0;
    sum_pool_borrow_balance = 0;
    pool_borrow_balance = Map.create();
}

(* No assumptions are made about the matching engine, it only needs to return an address. *)
val matched_address () : address

(* No assumptions are made about the gas consumption. *)
val iterations () : int

(* To force the invariant to be verified in the logic *)
predicate type_invariant_hold (_: pool_storage) (_: morpho_storage) = true

(* If a mapping contains only non-negative numbers, then for any set of keys, the sum of all the corresponding values is non-negative *)
predicate sum_of_positive (set: fset address) =
    forall m. (forall a. get a m >= 0) ->
        FsetSum.sum set (fun x -> get x m) >= 0
clone set.FsetInduction with type t=address, predicate p=sum_of_positive
lemma sum_of_positive_holds:
    forall set. sum_of_positive set

(* The sum of the p2p supply balances is greater than any account's *)
let lemma aggregate_p2p_supply_balance (m: morpho_storage) (a: address)
    ensures { let b = m.p2p_supply_balance in
    get a b <= FsetSum.sum b.Map.domain (fun x -> get x b)
    by if Map.mem a b
       then b.Map.domain = Fset.add a (Fset.remove a b.Map.domain)
       else sum_of_positive b.Map.domain
    }
= ()

(* The sum of pool supply balances is non-negative *)
let lemma aggregate_pool_supply_balance (m: morpho_storage)
    ensures { let b = m.pool_supply_balance in
    0 <= FsetSum.sum b.Map.domain (fun x -> get x b)
    by sum_of_positive b.Map.domain }
= ()

(* The sum of collateral supply balances is non-negative *)
let lemma aggregate_collateral_supply_balance (m: morpho_storage)
    ensures { let b = m.collateral_supply_balance in
    0 <= FsetSum.sum b.Map.domain (fun x -> get x b)
    by sum_of_positive b.Map.domain }
= ()

(* The sum of p2p borrow balances is greater than any account's *)
let lemma aggregate_p2p_borrow_balance (m: morpho_storage) (e: address)
    ensures { let b = m.p2p_borrow_balance in
    get e b <= FsetSum.sum b.Map.domain (fun x -> get x b)
    by if Map.mem e b
       then b.Map.domain = Fset.add e (Fset.remove e b.Map.domain)
       else sum_of_positive b.Map.domain
    }
= ()

(* The sum of pool borrow balances is non-negative *)
let lemma aggregate_pool_borrow_balance (m: morpho_storage)
    ensures { let b = m.pool_borrow_balance in
    0 <= FsetSum.sum b.Map.domain (fun x -> get x b)
    by sum_of_positive b.Map.domain }
= ()

let map_add (user: address) (amount: int) (map: Map.t int)
    ensures { get user map = amount }
    ensures { forall a. a <> user -> get a map = get a (old map) }
    ensures { FsetSum.sum map.Map.domain (fun x -> get x map) = FsetSum.sum (old map).Map.domain (fun x -> get x (old map)) + amount - get user (old map) }
=
    Map.add user amount map;
    assert { map.Map.domain = Fset.add user (Fset.remove user (old map.Map.domain)) }

(* The sum of p2p and pool supply balance of a user *)
let function supply_balance (m: morpho_storage) (user: address) : int
= get user m.p2p_supply_balance + get user m.pool_supply_balance

(* The sum of p2p and pool borrow balance of a user *)
let function borrow_balance (m: morpho_storage) (user: address) : int
= get user m.p2p_borrow_balance + get user m.pool_borrow_balance

(* Change a supplier's pool and p2p position and update the total amounts *)
let update_supplier (m: morpho_storage) (user: address) (p2p_amount: int) (pool_amount: int)
    writes { m.p2p_supply_amount, m.p2p_supply_balance, m.sum_pool_supply_balance, m.pool_supply_balance }
    requires { p2p_amount >= 0 && pool_amount >= 0 }
    ensures { let former_p2p = get user (old m).p2p_supply_balance in
              m.p2p_supply_amount = (old m).p2p_supply_amount + p2p_amount - former_p2p }
    ensures { forall a. a <> user -> get a m.p2p_supply_balance = get a (old m.p2p_supply_balance) }
    ensures { get user m.p2p_supply_balance = p2p_amount }
    ensures { let former_pool = get user (old m).pool_supply_balance in
              m.sum_pool_supply_balance = (old m).sum_pool_supply_balance + pool_amount - former_pool }
    ensures { forall a. a <> user -> get a m.pool_supply_balance = get a (old m.pool_supply_balance) }
    ensures { get user m.pool_supply_balance = pool_amount }
    ensures { supply_balance m user = p2p_amount + pool_amount }
=
    let former_p2p = get user m.p2p_supply_balance in
    let former_pool = get user m.pool_supply_balance in
    m.p2p_supply_amount <- m.p2p_supply_amount + p2p_amount - former_p2p;
    map_add user p2p_amount m.p2p_supply_balance;
    m.sum_pool_supply_balance <- m.sum_pool_supply_balance + pool_amount - former_pool;
    map_add user pool_amount m.pool_supply_balance

(* Change a borrower's pool and p2p position and update the total amounts *)
let update_borrower (m: morpho_storage) (user: address) (p2p_amount: int) (pool_amount: int)
    writes { m.p2p_borrow_amount, m.p2p_borrow_balance, m.sum_pool_borrow_balance, m.pool_borrow_balance }
    requires { p2p_amount >= 0 && pool_amount >= 0 }
    ensures { let former_p2p = get user (old m).p2p_borrow_balance in
              m.p2p_borrow_amount = (old m).p2p_borrow_amount + p2p_amount - former_p2p }
    ensures { forall a. a <> user -> get a m.p2p_borrow_balance = get a (old m.p2p_borrow_balance) }
    ensures { get user m.p2p_borrow_balance = p2p_amount }
    ensures { let former_pool = get user (old m).pool_borrow_balance in
              m.sum_pool_borrow_balance = (old m).sum_pool_borrow_balance + pool_amount - former_pool }
    ensures { forall a. a <> user -> get a m.pool_borrow_balance = get a (old m.pool_borrow_balance) }
    ensures { get user m.pool_borrow_balance = pool_amount }
    ensures { borrow_balance m user = p2p_amount + pool_amount }
=
    let former_p2p = get user m.p2p_borrow_balance in
    let former_pool = get user m.pool_borrow_balance in
    m.p2p_borrow_amount <- m.p2p_borrow_amount + p2p_amount - former_p2p;
    map_add user p2p_amount m.p2p_borrow_balance;
    m.sum_pool_borrow_balance <- m.sum_pool_borrow_balance + pool_amount - former_pool;
    map_add user pool_amount m.pool_borrow_balance

(* Percentage operations are done on basis points. *)
let constant percentage_factor = 100_00

(* Multiplication by a percentage, named percentMulDown in the code. *)
let function mul (x y: int) : int = div (x * y) percentage_factor

let lemma div_add (x y z: int)
    requires { x >= 0 && y >= 0 && z > 0 }
    ensures { div (x + y) z >= div x z + div y z
              by let qx = div x z in let rx = mod x z in
                 let qy = div y z in let ry = mod y z in
                 let qs = div (x + y) z in let rs = mod (x + y) z in
                 x = qx * z + rx &&
                 y = qy * z + ry &&
                 x + y = qs * z + rs
                 so (qs - qx - qy) * z = rx + ry - rs
                 so (qs - qx - qy) * z > - z
                 so (qs - qx - qy <= -1 -> (qs - qx - qy) * z <= -1 * z)
    }
= ()

let lemma mul_add (x y z: int)
    requires { x >= 0 && y >= 0 && z >= 0 }
    ensures { mul x z + mul y z <= mul (x + y) z }
= div_add (x * z) (y * z) percentage_factor

let lemma mul_right_increasing (x y z: int)
    requires { x >= 0 && y >= 0 && z >= 0 }
    requires { x >= y }
    ensures { mul x z >= mul y z }
= mul_add (x - y) y z

(* The liquidation threshold, a factor smaller than 1. *)
val constant lt : int
    ensures { 0 <= result < percentage_factor }

(* All invariants verified by the main functions: supply, borrow, ... *)
predicate well_formed (t: token_storage) (p: pool_storage) (m: morpho_storage) =
    p.supply_on_pool = m.sum_pool_supply_balance + m.sum_collateral_supply_balance + m.p2p_supply_delta &&
    p.borrow_on_pool = m.sum_pool_borrow_balance + m.p2p_borrow_delta &&
    m.p2p_supply_delta + m.idle_supply <= m.p2p_supply_amount &&
    m.p2p_borrow_delta <= m.p2p_borrow_amount &&
    m.p2p_supply_amount - m.p2p_supply_delta - m.idle_supply = m.p2p_borrow_amount - m.p2p_borrow_delta &&
    m.idle_supply = get morpho t.balance &&
    forall user. mul (get user m.collateral_supply_balance) lt >= borrow_balance m user
end

module NonLiquidation
use MorphoInternal
let rec lemma mul_sum (m: morpho_storage) (s: Fset.fset address)
    variant { Fset.cardinal s }
    ensures { FsetSum.sum s (fun a -> mul (get a m.collateral_supply_balance) lt) <= mul (FsetSum.sum s (fun a -> get a m.collateral_supply_balance)) lt }
= if not (Fset.is_empty s) then begin
  let e = Fset.pick s in
  mul_sum m (Fset.remove e s);
  assert { FsetSum.sum (Fset.remove e s) (fun a -> get a m.collateral_supply_balance) >= 0
           by sum_of_positive (Fset.remove e s)
  };
  assert SumMulRemove { FsetSum.sum s (fun a -> mul (get a m.collateral_supply_balance) lt) =
           mul (get e m.collateral_supply_balance) lt +
           FsetSum.sum (Fset.remove e s) (fun a -> mul (get a m.collateral_supply_balance) lt) };
  assert SumRemove { FsetSum.sum s (fun a -> get a m.collateral_supply_balance) =
           get e m.collateral_supply_balance +
           FsetSum.sum (Fset.remove e s) (fun a -> get a m.collateral_supply_balance) };
  assert MulAdd { mul (get e m.collateral_supply_balance) lt + mul (FsetSum.sum (Fset.remove e s) (fun a -> get a m.collateral_supply_balance)) lt <= mul (get e m.collateral_supply_balance + FsetSum.sum (Fset.remove e s) (fun a -> get a m.collateral_supply_balance)) lt }
  end

let rec lemma sum_greater (t: token_storage) (p: pool_storage) (m: morpho_storage) (s: Fset.fset address)
    variant { Fset.cardinal s }
    requires { well_formed t p m }
    ensures { FsetSum.sum s (fun a -> mul (get a m.collateral_supply_balance) lt) >=
              FsetSum.sum s (fun a -> borrow_balance m a) }
= if not (Fset.is_empty s) then
  let e = Fset.pick s in
  sum_greater t p m (Fset.remove e s)

let rec lemma sum_add (m: morpho_storage) (s: Fset.fset address)
    variant { Fset.cardinal s }
    ensures { FsetSum.sum s (fun a -> borrow_balance m a) =
              FsetSum.sum s (fun a -> get a m.pool_borrow_balance) +
              FsetSum.sum s (fun a -> get a m.p2p_borrow_balance) }
= if not (Fset.is_empty s) then begin
  let e = Fset.pick s in
  sum_add m (Fset.remove e s);
  assert { FsetSum.sum s (fun a -> borrow_balance m a) =
           borrow_balance m e +
           FsetSum.sum (Fset.remove e s) (fun a -> borrow_balance m a) };
  assert { FsetSum.sum s (fun a -> get a m.pool_borrow_balance) =
           get e m.pool_borrow_balance+
           FsetSum.sum (Fset.remove e s) (fun a -> get a m.pool_borrow_balance) };
  assert { FsetSum.sum s (fun a -> get a m.p2p_borrow_balance) =
           get e m.p2p_borrow_balance+
           FsetSum.sum (Fset.remove e s) (fun a -> get a m.p2p_borrow_balance)
           by FsetSum.sum s (fun a -> get a m.p2p_borrow_balance) =
              FsetSum.sum (Fset.add e (Fset.remove e s)) (fun a -> get a m.p2p_borrow_balance)
           }
  end

let rec lemma sum_domain (s: Fset.fset address) (m: Map.t int)
    variant { Fset.cardinal s - Fset.cardinal m.Map.domain }
    requires { Fset.subset m.Map.domain s }
    ensures { FsetSum.sum s (fun x -> get x m) = FsetSum.sum m.Map.domain (fun x -> get x m) }
= if not (Fset.cardinal s = Fset.cardinal (Map.to_fmap m).Map.domain) then begin
  let e = Fset.pick (Fset.diff s (Map.to_fmap m).Map.domain) in
  sum_domain (Fset.remove e s) m;
  assert { FsetSum.sum s (fun x -> get x m) =
           get e m + FsetSum.sum (Fset.remove e s) (fun x -> get x m) };
  assert { get e m = 0 }
  end

let lemma sum_collateralization_users (t: token_storage) (p: pool_storage) (m: morpho_storage)
    requires { well_formed t p m }
    ensures { mul m.sum_collateral_supply_balance lt >= m.sum_pool_borrow_balance + m.p2p_borrow_amount }
=
  assert { m.sum_collateral_supply_balance = FsetSum.sum m.collateral_supply_balance.Map.domain (fun x -> get x m.collateral_supply_balance) };
  assert { m.sum_pool_borrow_balance = FsetSum.sum m.pool_borrow_balance.Map.domain (fun x -> get x m.pool_borrow_balance) };
  assert { m.p2p_borrow_amount = FsetSum.sum m.p2p_borrow_balance.Map.domain (fun x -> get x m.p2p_borrow_balance) };
  let s = Fset.union (Map.to_fmap m.collateral_supply_balance).Map.domain (Fset.union (Map.to_fmap m.pool_borrow_balance).Map.domain (Map.to_fmap m.p2p_borrow_balance).Map.domain) in
  mul_sum m s;
  sum_greater t p m s;
  sum_add m s;
  sum_domain s m.collateral_supply_balance;
  sum_domain s m.pool_borrow_balance;
  sum_domain s m.p2p_borrow_balance;
  assert SumCollateral { m.sum_collateral_supply_balance = FsetSum.sum s (fun x -> get x m.collateral_supply_balance) };
  assert SumPoolBorrow { m.sum_pool_borrow_balance = FsetSum.sum s (fun x -> get x m.pool_borrow_balance) };
  assert SumP2PBorrow { m.p2p_borrow_amount = FsetSum.sum s (fun x -> get x m.p2p_borrow_balance) }

(* The non liquidation theorem, assuming in particular that each user is enough collateralized *)
let lemma non_liquidation_theorem (t: token_storage) (p: pool_storage) (m: morpho_storage)
    requires { well_formed t p m }
    ensures { mul p.supply_on_pool lt >= p.borrow_on_pool }
= aggregate_pool_supply_balance m
end

module Supply
use MorphoInternal
let supply (t: token_storage) (p: pool_storage) (m: morpho_storage) (user: address) (amount: int)
    (* Untouched: m.p2p_supply_delta, m.idle_supply, m.sum_collateral_supply_balance, m.collateral_supply_balance *)
    writes { t.balance, p.suppliable, p.supply_on_pool, p.borrow_on_pool, m.p2p_supply_amount, m.p2p_supply_balance, m.sum_pool_supply_balance, m.pool_supply_balance, m.p2p_borrow_amount, m.p2p_borrow_balance, m.p2p_borrow_delta, m.sum_pool_borrow_balance, m.pool_borrow_balance }
    requires { amount >= 0 }
    requires { user <> morpho && get user t.balance >= amount }
    requires { well_formed t p m }
    ensures { well_formed t p m }
    ensures { supply_balance m user = supply_balance (old m) user + amount }
    raises { Underflow -> false }
    raises { SupplyCapReached }
=
    transfer_from t user morpho amount;

    let former_p2p = get user m.p2p_supply_balance in
    let former_pool = get user m.pool_supply_balance in
    assert { supply_balance m user = former_pool + former_p2p };
    let ref to_supply = amount in
    let ref to_repay = 0 in

    (* Match peer-to-peer borrow delta *)
    let delta_matched = min m.p2p_borrow_delta to_supply in
    m.p2p_borrow_delta <- m.p2p_borrow_delta - delta_matched;
    to_supply <- to_supply - delta_matched;
    to_repay <- to_repay + delta_matched;

    (* Promote borrowers *)
    if to_supply > 0 then
    for i = 1 to iterations () do
        invariant { to_supply >= 0 && to_repay >= 0 }
        invariant { to_supply + to_repay = amount }
        invariant { p.borrow_on_pool = m.sum_pool_borrow_balance + to_repay }
        invariant { m.p2p_borrow_amount = (old m).p2p_borrow_amount + to_repay - delta_matched }
        invariant { forall u. borrow_balance m u = borrow_balance (old m) u }
        invariant { type_invariant_hold p m }
        let borrower = matched_address() in
        let borrower_p2p = get borrower m.p2p_borrow_balance in
        let borrower_pool = get borrower m.pool_borrow_balance in
        let borrower_matched = min to_supply borrower_pool in
        update_borrower m borrower (borrower_p2p + borrower_matched) (borrower_pool - borrower_matched);
        to_supply <- to_supply - borrower_matched;
        to_repay <- to_repay + borrower_matched;
    done;
    assert { m.sum_pool_borrow_balance = FsetSum.sum m.pool_borrow_balance.Map.domain (fun x -> get x m.pool_borrow_balance) };

    (* Pool interaction *)
    repay_to_pool t p to_repay;
    supply_to_pool t p to_supply;

    update_supplier m user (former_p2p + to_repay) (former_pool + to_supply)
end

module SupplyCollateral
use MorphoInternal
let supply_collateral (t: token_storage) (p: pool_storage) (m: morpho_storage) (user: address) (amount: int)
    (* Untouched: p.borrow_on_pool, m.p2p_supply_amount, m.p2p_supply_balance, m.sum_pool_supply_balance, m.pool_supply_balance, m.idle_supply, m.p2p_borrow_amount, m.p2p_borrow_balance, m.p2p_borrow_delta, m.p2p_supply_delta, m.sum_pool_borrow_balance, m.pool_borrow_balance *)
    writes { t.balance, p.suppliable, p.supply_on_pool, m.sum_collateral_supply_balance, m.collateral_supply_balance }
    requires { amount >= 0 }
    requires { user <> morpho && get user t.balance >= amount }
    requires { well_formed t p m }
    ensures { well_formed t p m }
    ensures { get user m.collateral_supply_balance = get user (old m).collateral_supply_balance + amount }
    raises { SupplyCapReached }
=
    transfer_from t user morpho amount;

    (* execute supply collateral *)
    let former_collateral = get user m.collateral_supply_balance in
    m.sum_collateral_supply_balance <- m.sum_collateral_supply_balance + amount;
    map_add user (former_collateral + amount) m.collateral_supply_balance;

    supply_to_pool t p amount;

    mul_right_increasing (get user m.collateral_supply_balance) former_collateral lt;
    assert { forall user'.
             let res = mul (get user' m.collateral_supply_balance) lt >= borrow_balance m user' in
             res by if user' = user then res else res
    }
end

module Borrow
use MorphoInternal
let borrow (t: token_storage) (p: pool_storage) (m: morpho_storage) (user: address) (amount: int)
    (* Untouched: m.sum_collateral_supply_balance, m.collateral_supply_balance, m.p2p_borrow_delta *)
    writes { t.balance, p.suppliable, p.supply_on_pool, p.borrow_on_pool, m.p2p_supply_amount, m.p2p_supply_balance, m.p2p_supply_delta, m.sum_pool_supply_balance, m.pool_supply_balance, m.idle_supply, m.p2p_borrow_amount, m.p2p_borrow_balance, m.sum_pool_borrow_balance, m.pool_borrow_balance }
    requires { amount >= 0 }
    requires { user <> morpho && get pool t.balance >= amount }
    requires { well_formed t p m }
    ensures { well_formed t p m }
    ensures { borrow_balance m user = borrow_balance (old m) user + amount }
    raises { Underflow -> false }
    raises { Unauthorized }
=
    let former_p2p = get user m.p2p_borrow_balance in
    let former_pool = get user m.pool_borrow_balance in
    assert { borrow_balance m user = former_pool + former_p2p };

    let collateral = get user m.collateral_supply_balance in
    if mul collateral lt < borrow_balance m user + amount then raise Unauthorized;

    let ref to_borrow = amount in
    let ref to_withdraw = 0 in

    (* Decrease the peer-to-peer idle supply *)
    let matched_idle = min amount m.idle_supply in
    to_borrow <- to_borrow - matched_idle;
    m.idle_supply <- m.idle_supply - matched_idle;

    (* Match peer-to-peer supply delta *)
    let delta_matched = min m.p2p_supply_delta to_borrow in
    m.p2p_supply_delta <- m.p2p_supply_delta - delta_matched;
    to_borrow <- to_borrow - delta_matched;
    to_withdraw <- to_withdraw + delta_matched;

    (* Promote suppliers *)
    if to_borrow > 0 then
    for i = 1 to iterations () do
        invariant { to_borrow >= 0 && to_withdraw >= 0 }
        invariant { to_borrow + to_withdraw + matched_idle = amount }
        invariant { p.supply_on_pool = m.sum_pool_supply_balance + m.sum_collateral_supply_balance + to_withdraw }
        invariant { m.p2p_supply_amount = (old m).p2p_supply_amount + to_withdraw - delta_matched }
        invariant { type_invariant_hold p m }
        let supplier = matched_address() in
        let supplier_p2p = get supplier m.p2p_supply_balance in
        let supplier_pool = get supplier m.pool_supply_balance in
        let supplier_matched = min to_borrow supplier_pool in
        update_supplier m supplier (supplier_p2p + supplier_matched) (supplier_pool - supplier_matched);
        to_borrow <- to_borrow - supplier_matched;
        to_withdraw <- to_withdraw + supplier_matched;
    done;
    assert { m.sum_pool_borrow_balance = FsetSum.sum m.pool_borrow_balance.Map.domain (fun x -> get x m.pool_borrow_balance) };
    assert { m.sum_collateral_supply_balance = FsetSum.sum m.collateral_supply_balance.Map.domain (fun x -> get x m.collateral_supply_balance) };

    (* Pool interaction *)
    withdraw_from_pool t p to_withdraw;
    borrow_from_pool t p to_borrow;

    update_borrower m user (former_p2p + to_withdraw + matched_idle) (former_pool + to_borrow);

    transfer_from t morpho user amount
end

module Withdraw
use MorphoInternal
let withdraw (t: token_storage) (p: pool_storage) (m: morpho_storage) (user: address) (amount: int)
    (* Untouched: m.sum_collateral_supply_balance, m.collateral_supply_balance *)
    writes { t.balance, p.suppliable, p.supply_on_pool, p.borrow_on_pool, m.p2p_supply_amount, m.p2p_supply_balance, m.p2p_supply_delta, m.sum_pool_supply_balance, m.pool_supply_balance, m.idle_supply, m.p2p_borrow_amount, m.p2p_borrow_balance, m.p2p_borrow_delta, m.sum_pool_borrow_balance, m.pool_borrow_balance }
    requires { amount >= 0 }
    requires { user <> morpho && get pool t.balance >= amount }
    requires { well_formed t p m }
    ensures { well_formed t p m }
    ensures { supply_balance m user = supply_balance (old m) user - amount }
    raises { Underflow -> amount > supply_balance (old m) user  }
=
    let former_p2p = get user m.p2p_supply_balance in
    let former_pool = get user m.pool_supply_balance in
    let former_balance = former_pool + former_p2p in

    if amount > former_balance then raise Underflow;

    let ref remaining = amount in
    let ref to_withdraw = 0 in
    let ref to_borrow = 0 in

    (* Pool withdraw *)
    let withdrawn_from_pool = min remaining former_pool in
    remaining <- remaining - withdrawn_from_pool;
    to_withdraw <- to_withdraw + withdrawn_from_pool;

    aggregate_p2p_supply_balance m user;
    assert { remaining <= m.p2p_supply_amount };

    update_supplier m user (former_p2p - remaining) (former_pool - withdrawn_from_pool);

    (* Decrease idle supply *)
    let matched_idle = min remaining m.idle_supply in
    remaining <- remaining - matched_idle;
    m.idle_supply <- m.idle_supply - matched_idle;

    (* Reduce peer-to-peer supply delta *)
    let delta_matched = min remaining m.p2p_supply_delta in
    m.p2p_supply_delta <- m.p2p_supply_delta - delta_matched;
    remaining <- remaining - delta_matched;
    to_withdraw <- to_withdraw + delta_matched;

    assert { m.p2p_supply_amount >= m.p2p_supply_delta + m.idle_supply };

    (* Promote suppliers *)
    for i = 1 to iterations () do
        invariant { remaining >= 0 && to_withdraw >= 0 }
        invariant { remaining + to_withdraw + matched_idle = amount }
        invariant { p.supply_on_pool = m.sum_pool_supply_balance + m.sum_collateral_supply_balance + m.p2p_supply_delta + to_withdraw  }
        invariant { m.p2p_supply_amount >= m.p2p_supply_delta + m.idle_supply }
        invariant { m.p2p_supply_amount = (old m).p2p_supply_amount - remaining - delta_matched - matched_idle }
        invariant { supply_balance m user = supply_balance (old m) user - amount } (* a trick to not have to specify the matching engine *)
        invariant { type_invariant_hold p m }
        let supplier = matched_address() in
        let supplier_p2p = get supplier m.p2p_supply_balance in
        let supplier_pool = get supplier m.pool_supply_balance in
        let supplier_matched = min remaining supplier_pool in
        update_supplier m supplier (supplier_p2p + supplier_matched) (supplier_pool - supplier_matched);
        remaining <- remaining - supplier_matched;
        to_withdraw <- to_withdraw + supplier_matched;
    done;
    assert { m.sum_pool_supply_balance = FsetSum.sum m.pool_supply_balance.Map.domain (fun x -> get x m.pool_supply_balance) };
    assert { m.sum_collateral_supply_balance = FsetSum.sum m.collateral_supply_balance.Map.domain (fun x -> get x m.collateral_supply_balance) };

    (* Demote borrowers *)
    for i = 1 to iterations () do
        invariant { remaining >= 0 && to_withdraw >= 0 && to_borrow >= 0 }
        invariant { remaining + to_withdraw + to_borrow + matched_idle = amount }
        invariant { p.borrow_on_pool = m.sum_pool_borrow_balance + m.p2p_borrow_delta - to_borrow }
        invariant { m.p2p_borrow_amount = (old m).p2p_borrow_amount - to_borrow }
        invariant { forall u. borrow_balance m u = borrow_balance (old m) u }
        invariant { type_invariant_hold p m }
        let borrower = matched_address() in
        let borrower_p2p = get borrower m.p2p_borrow_balance in
        let borrower_pool = get borrower m.pool_borrow_balance in
        let borrower_unmatched = min remaining borrower_p2p in
        update_borrower m borrower (borrower_p2p - borrower_unmatched) (borrower_pool + borrower_unmatched);
        remaining <- remaining - borrower_unmatched;
        to_borrow <- to_borrow + borrower_unmatched;
    done;

    (* Increase peer-to-peer borrow delta *)
    m.p2p_borrow_delta <- m.p2p_borrow_delta + remaining;
    to_borrow <- to_borrow + remaining;

    (* Pool interaction *)
    withdraw_from_pool t p to_withdraw;
    borrow_from_pool t p to_borrow;

    transfer_from t morpho user amount
end

module WithdrawCollateral
use MorphoInternal
let withdraw_collateral (t: token_storage) (p: pool_storage) (m: morpho_storage) (user: address) (amount: int)
    (* Untouched: p.borrow_on_pool, m.p2p_supply_amount, m.p2p_supply_balance, m.sum_pool_supply_balance, m.pool_supply_balance, m.idle_supply, m.p2p_borrow_amount, m.p2p_borrow_balance, m.p2p_borrow_delta, m.p2p_supply_delta, m.sum_pool_borrow_balance, m.pool_borrow_balance *)
    writes { t.balance, p.suppliable, p.supply_on_pool, m.sum_collateral_supply_balance, m.collateral_supply_balance }
    requires { amount >= 0 }
    requires { user <> morpho && get pool t.balance >= amount }
    requires { well_formed t p m }
    ensures { well_formed t p m }
    ensures { get user m.collateral_supply_balance = get user (old m).collateral_supply_balance - amount }
    raises { Underflow -> amount > get user (old m).collateral_supply_balance }
    raises { Unauthorized }
=
    let former_collateral = get user m.collateral_supply_balance in
    if amount > former_collateral then raise Underflow;
    if mul (former_collateral - amount) lt < borrow_balance m user then raise Unauthorized;

    (* execute withdraw *)
    m.sum_collateral_supply_balance <- m.sum_collateral_supply_balance - amount;
    map_add user (former_collateral - amount) m.collateral_supply_balance;

    withdraw_from_pool t p amount;

    transfer_from t morpho user amount
end

module Repay
use MorphoInternal
let repay (t: token_storage) (p: pool_storage) (m: morpho_storage) (user: address) (amount: int)
    (* Untouched: m.sum_collateral_supply_balance, m.collateral_supply_balance *)
    writes { t.balance, p.suppliable, p.supply_on_pool, p.borrow_on_pool, m.p2p_supply_amount, m.p2p_supply_balance, m.p2p_supply_delta, m.sum_pool_supply_balance, m.pool_supply_balance, m.idle_supply, m.p2p_borrow_amount, m.p2p_borrow_balance, m.p2p_borrow_delta, m.sum_pool_borrow_balance, m.pool_borrow_balance }
    requires { amount >= 0 }
    requires { user <> morpho && get user t.balance >= amount }
    requires { well_formed t p m }
    ensures { well_formed t p m }
    ensures { borrow_balance m user = borrow_balance (old m) user - amount }
    raises { Underflow -> amount > borrow_balance (old m) user }
    raises { SupplyCapReached -> false }
=
    transfer_from t user morpho amount;

    let former_p2p = get user m.p2p_borrow_balance in
    let former_pool = get user m.pool_borrow_balance in
    let former_balance = former_pool + former_p2p in

    if amount > former_balance then raise Underflow;

    let ref remaining = amount in
    let ref to_repay = 0 in
    let ref to_supply = 0 in

    (* Pool repay *)
    let repaid_to_pool = min remaining former_pool in
    remaining <- remaining - repaid_to_pool;
    to_repay <- to_repay + repaid_to_pool;

    aggregate_p2p_borrow_balance m user;
    assert { remaining <= m.p2p_borrow_amount };

    update_borrower m user (former_p2p - remaining) (former_pool - repaid_to_pool);

    (* Reduce peer-to-peer borrow delta *)
    let delta_matched = min remaining m.p2p_borrow_delta in
    m.p2p_borrow_delta <- m.p2p_borrow_delta - delta_matched;
    remaining <- remaining - delta_matched;
    to_repay <- to_repay + delta_matched;

    assert { m.p2p_borrow_amount >= m.p2p_borrow_delta };

    (* Promote borrowers *)
    for i = 1 to iterations () do
        invariant { remaining >= 0 && to_repay >= 0 }
        invariant { remaining + to_repay = amount }
        invariant { p.borrow_on_pool = m.sum_pool_borrow_balance + m.p2p_borrow_delta + to_repay  }
        invariant { m.p2p_borrow_amount >= m.p2p_borrow_delta }
        invariant { m.p2p_borrow_amount = (old m).p2p_borrow_amount - remaining - delta_matched }
        invariant { borrow_balance m user = borrow_balance (old m) user - amount }
        invariant { forall u. borrow_balance m u <= borrow_balance (old m) u }
        invariant { type_invariant_hold p m }
        let borrower = matched_address() in
        let borrower_p2p = get borrower m.p2p_borrow_balance in
        let borrower_pool = get borrower m.pool_borrow_balance in
        let borrower_matched = min remaining borrower_pool in
        update_borrower m borrower (borrower_p2p + borrower_matched) (borrower_pool - borrower_matched);
        remaining <- remaining - borrower_matched;
        to_repay <- to_repay + borrower_matched;
    done;
    assert { m.sum_pool_borrow_balance = FsetSum.sum m.pool_borrow_balance.Map.domain (fun x -> get x m.pool_borrow_balance) };
    assert { m.p2p_borrow_amount = FsetSum.sum m.p2p_borrow_balance.Map.domain (fun x -> get x m.p2p_borrow_balance) };

    (* Increase idle supply *)
    let increased_idle = max 0 (remaining - p.suppliable) in
    m.idle_supply <- m.idle_supply + increased_idle;
    remaining <- remaining - increased_idle;

    (* Demote suppliers *)
    for i = 1 to iterations () do
        invariant { remaining >= 0 && to_repay >= 0 && to_supply >= 0 }
        invariant { remaining + increased_idle + to_repay + to_supply = amount }
        invariant { p.supply_on_pool = m.sum_pool_supply_balance + m.sum_collateral_supply_balance + m.p2p_supply_delta - to_supply }
        invariant { m.p2p_supply_amount = (old m).p2p_supply_amount - to_supply }
        invariant { remaining + to_supply <= p.suppliable }
        invariant { forall u. borrow_balance m u <= borrow_balance (old m) u }
        invariant { type_invariant_hold p m }
        let supplier = matched_address() in
        let supplier_p2p = get supplier m.p2p_supply_balance in
        let supplier_pool = get supplier m.pool_supply_balance in
        let supplier_unmatched = min remaining supplier_p2p in
        update_supplier m supplier (supplier_p2p - supplier_unmatched) (supplier_pool + supplier_unmatched);
        remaining <- remaining - supplier_unmatched;
        to_supply <- to_supply + supplier_unmatched;
    done;
    assert { m.sum_collateral_supply_balance = FsetSum.sum m.collateral_supply_balance.Map.domain (fun x -> get x m.collateral_supply_balance) };
    assert { remaining + to_supply <= p.suppliable };

    (* Increase peer-to-peer supply delta *)
    m.p2p_supply_delta <- m.p2p_supply_delta + remaining;
    to_supply <- to_supply + remaining;

    (* Pool interaction *)
    repay_to_pool t p to_repay;
    supply_to_pool t p to_supply
end

module IncreaseP2PDeltas
use MorphoInternal
let increase_p2p_deltas (t: token_storage) (p: pool_storage) (m: morpho_storage) (amount: int) : (increased: int)
    requires { amount >= 0 }
    requires { well_formed t p m }
    requires { get pool t.balance >= amount }
    ensures { well_formed t p m }
    ensures { m.p2p_supply_delta = (old m).p2p_supply_delta + increased }
    ensures { m.p2p_borrow_delta = (old m).p2p_borrow_delta + increased }
    raises { SupplyCapReached -> amount > (old p).suppliable }
=
    let true_supply = m.p2p_supply_amount - m.p2p_supply_delta - m.idle_supply in
    let true_borrow = m.p2p_borrow_amount - m.p2p_borrow_delta in
    let increased = min amount (min true_supply true_borrow) in

    if increased = 0 then return 0;

    (* Increase deltas *)
    m.p2p_supply_delta <- m.p2p_supply_delta + increased;
    m.p2p_borrow_delta <- m.p2p_borrow_delta + increased;

    (* Interact with the pool accordingly *)
    borrow_from_pool t p increased;
    supply_to_pool t p increased;

    return increased
end
